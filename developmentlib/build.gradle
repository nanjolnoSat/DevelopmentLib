apply plugin: 'com.android.library'
apply from: "$rootDir/kotlin_plugin.gradle"
apply from: "$rootDir/java_compile_version.gradle"

android {
    def modulePackage = "com.mishaki.lib"

    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures {
        dataBinding useDataBinding
    }

    sourceSets {
        main {
            java {
                //可以使用以下几种方式忽略文件或目录
                //exclude getDir4PackageName(modulePackage + ".extensions")
                //exclude(getDir4PackageName(modulePackage + ".extensions"), "xxx/xxx/xxx.java", "xxx/xxx/xxxx.kt")
                //exclude getFileDirList(getDir4PackageName(modulePackage),"a.java","b.kt")

                //当不使用kotlin的时候，就不编译kotlin相关的文件
                if (!useKotlin) {
                    exclude getDir4PackageName(modulePackage + ".extensions")
                }
                //当不使用DataBinding的时候，就不编译DataBinding相关的文件
                if (!useDataBinding) {
                    //code...
                }
            }
            res {
                //当不使用DataBinding的时候，就不编译DataBinding相关的文件
                if (!useDataBinding) {
                    //code...
                }
            }
        }
    }

    buildTypes {
        def USE_ROOM = "USE_ROOM"

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", USE_ROOM, String.valueOf(useRoom)
        }

        debug {
            buildConfigField "boolean", USE_ROOM, String.valueOf(useRoom)
        }
    }
}

static String getDir4PackageName(String packageName) {
    if (packageName == null) {
        return ""
    }
    String[] strList = packageName.split("\\.")
    if (strList.length == 0) {
        return ""
    }
    String dir = ""
    for (String str : strList) {
        dir += str + "/"
    }
    return dir
}

static String[] getFileDirList(String dir, String... fileNames) {
    if (fileNames == null || fileNames.length == 0) {
        return new String[0];
    }
    String[] dirList = new String[fileNames.length];
    for (int i = 0; i < fileNames.length; i++) {
        dirList[i] = dir + fileNames[i];
    }
    return dirList;
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation deps.androidCore

    //kotlin协程
    if (useKotlin && useKotlinCoroutine){
        api deps.kotlinCoroutineCore
        api deps.kotlinCoroutineCoreAndroid
    }
    //自己的module
    api project(path: ':sdkpi')
    //是否使用Room
    if (useRoom) {
        api project(path: ':roomLib')
    } else {
        api project(path: ':roomLib_empty')
    }
}